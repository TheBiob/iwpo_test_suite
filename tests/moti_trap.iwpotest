; Tests for:
;  Save sharing
;  Saved message instance existing after receiving an online save
;  Deleting online saves when player saves

; Not currently tested:
;  gravity flipping (engine does not contain gravity flippers)
;  other player being visible and correctly oriented when facing left/right etc
;  save sharing between rooms
;  chat messages
;  disable online saves feature
;  save-delay preventing multiple save messages to the server when game autosaves every frame

[configuration]
folder=moti_trap
game=moti_trapv0.4.exe
output=moti_trapv0.4_online.exe
timeout=60
is_gms=false
skip_execute=false

[server_packages]
save_game=TCP 05 00 "test" 000000C0 1760.0 0070
saved_384_1760=TCP 05 00 "test2" 00000180 1760.0 0070
; room = 112 (0x70)

[events]
$$pre_worldCreate
	@name="Test";
	@password="";
	@race=false;

$$test_GameBegin
	if (@vars_loaded == false) {
		ds_list_add(@vars, 0);
	}
	@step = ds_list_find_value(@vars, 0);

$$test_Step
	@step += 1;
	ds_list_replace(@vars, 0, @step);

	if (@step == 2) {
		room_goto(rStage01);
	} else if (@step == 4) {
		@ASSERT(instance_exists(player), "object 'player' does not exist", true);
		@SERVER_EXPECT("save_game");
		with (player) {
			x = 192;
			y = 1760;
		}
		// Same thing the save point does, though in Step instead of On Collision with Bullet
		saveGame(0);
	} else if (@step == 6) {
		@SERVER_RESPOND("saved_384_1760");
	} else if (@step == 8) {
		if (instance_number(@playerSaved) != 1) {
			@ASSERTEQ(1, instance_number(@playerSaved), "saved instance count", false); // Will fail
		} else {
			@ASSERTEQ("test2 saved!", @playerSaved.@text, "saved text", false);
		}
		// Same thing pressing R does, though in Step instead of World/Button 'R' Pressed
		sound_stop_all();
		saveGame(true);
		loadGame();
	} else if (@step == 10) {
		@ASSERT(instance_exists(player), "object 'player' does not exist", true);
		@ASSERTEQ(384,    player.x, "player x 1", false);
		@ASSERTEQ(1760.4, player.y, "player y 1", false);
	} else if (@step == 12) {
		// Same thing pressing R does, though in Step instead of World/Button 'R' Pressed
		sound_stop_all();
		saveGame(true);
		loadGame();
	} else if (@step == 14) {
		// We loaded again without saving, player should be back on the original save
		@ASSERT(instance_exists(player), "object 'player' does not exist", true);
		@ASSERTEQ(192,    player.x, "player x 2", false);
		@ASSERTEQ(1760.4, player.y, "player y 2", false); // Seems like falling starts 1 frame later when loading an online save? Would've expected to see 1760+0.4+0.4*2 here instead.
		
		@SERVER_RESPOND("saved_384_1760");
	} else if (@step == 16) {
		@ASSERT(file_exists("tempOnline2"), "tempOnline2 does not exist", false);
		// Same thing the save point does, though in Step instead of On Collision with Bullet
		saveGame(0);
	} else if (@step == 18) {
		// Same thing pressing R does, though in Step instead of World/Button 'R' Pressed
		sound_stop_all();
		saveGame(true);
		loadGame();
	} else if (@step == 20) {
		// We received an online save, but then saved ourselves, reloading should take us to our save
		@ASSERT(instance_exists(player), "object 'player' does not exist", true);
		@ASSERTEQ(192, player.x, "player x 3", false);
		// Seems like here the player is falling for 3 frames? why was he falling for 1 frame the other times?
		// none of this makes sense to me but also it doesn't matter enough how many frames exactly the player fell.
		// If it's off by one or 2 when loading an online save, no one is going to notice.
		// Could be a problem if the online save logic changes but I guess that can be dealt with whenever this test breaks.
		@ASSERTEQ(1760+0.4+0.4*2+0.4*3, player.y, "player y 3", false);
	} else if (@step == 25) {
		@PASS();
	}
